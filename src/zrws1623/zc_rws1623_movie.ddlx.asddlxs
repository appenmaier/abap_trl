@Metadata.layer: #CUSTOMER
@UI.headerInfo:
{
  typeName: 'Movie',
  typeNamePlural: 'Movies',
  title.value: 'Title',
  description.value: 'PublishingYear',
  imageUrl: 'ImageUrl'
}
@UI.presentationVariant:
[
  { sortOrder:
    [
      { by: 'PublishingYear', direction: #DESC },
      { by: 'Title', direction: #ASC }
    ]
  }
]
annotate view ZC_RWS1623_Movie with
{

  /* Facets */
  @UI.facet:
  [
    { position: 10, type: #COLLECTION, id: 'Movie', label: 'Movie' },
    { position: 10, type: #FIELDGROUP_REFERENCE, parentId: 'Movie', targetQualifier: 'Details', label: 'Movie Details' },
    { position: 20, type: #FIELDGROUP_REFERENCE, parentId: 'Movie', targetQualifier: 'Admin', label: 'Administrative Data' },
    { position: 20, type: #LINEITEM_REFERENCE, targetElement: '_Ratings', label: 'Ratings'}
  ]

  /* Actions */
  @UI.lineItem:
  [
    { position: 5 },
    { position: 10, type: #FOR_ACTION, dataAction: 'SHOW_MESSAGE', label: 'Show Message' }
  ]
  @UI.identification: [{ position: 10, type: #FOR_ACTION, dataAction: 'RATE_MOVIE', label: 'Rate Movie' }]

  /* Fields */
  @UI.fieldGroup: [{ position: 10, qualifier: 'Admin' }]
  MovieUuid;

  @UI.lineItem: [{ position: 10 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'Details' }]
  Title;

  @UI.lineItem: [{ position: 20 }]
  @UI.selectionField: [{ position: 10 }]
  @UI.fieldGroup: [{ position: 20, qualifier: 'Details' }]
  Genre;

  @UI.lineItem: [{ position: 30 }]
  @UI.selectionField: [{ position: 20 }]
  @UI.fieldGroup: [{ position: 30, qualifier: 'Details' }]
  PublishingYear;

  @UI.lineItem: [{ position: 35, criticality: 'AverageRatingCriticality', criticalityRepresentation: #WITHOUT_ICON }]
  @UI.selectionField: [{ position: 25 }]
  @UI.fieldGroup: [{ position: 40, qualifier: 'Details' }]
  AverageRating;

  @UI.hidden: true
  AverageRatingCriticality;

  @UI.fieldGroup: [{ position: 40, qualifier: 'Details' }]
  ImageUrl;

  @UI.fieldGroup: [{ position: 20, qualifier: 'Admin' }]
  CreatedBy;

  @UI.fieldGroup: [{ position: 30, qualifier: 'Admin' }]
  CreatedAt;

  @UI.fieldGroup: [{ position: 40, qualifier: 'Admin' }]
  LastChangedBy;

  @UI.fieldGroup: [{ position: 50, qualifier: 'Admin' }]
  LastChangedAt;


}